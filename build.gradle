buildscript {
    repositories {
        maven { url "https://maven.fabricmc.net/" }
        jcenter()
    }
    dependencies {
        classpath "net.fabricmc:fabric-loom:0.2.7-SNAPSHOT"
    }
}

apply plugin: "fabric-loom"
apply plugin: "maven-publish"
apply plugin: "eclipse"

group = "com.terraforged"
version = "${fabric_mc_version}-${version}"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    minecraft "com.mojang:minecraft:${fabric_mc_version}"
    mappings "net.fabricmc:yarn:${fabric_mc_version}+build.${yarn_build}:v2"
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "FeatureManager-fabric",
                "Specification-Vendor"    : "FeatureManager-fabric",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "FeatureManager-fabric",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }

    if (System.getenv("MAVEN_USER") != null && System.getenv("MAVEN_PASS") != null) {
        repositories {
            maven {
                credentials {
                    username System.getenv("MAVEN_USER")
                    password System.getenv("MAVEN_PASS")
                }
                name = "nexus"
                url = "https://io.terraforged.com/repository/maven/"
            }
        }
    }
}